apply plugin: 'com.android.application'
apply plugin: 'realm-android'
android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.myinstagram"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            storeFile file("same-insta.jks")
            storePassword "shashank"
            keyAlias "key2"
            keyPassword "shashank"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".dev"
        }
        release {
            minifyEnabled true
            //shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles 'proguard-rules.pro'
            proguardFiles 'progurad-rules-glide.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    flavorDimensions "default"
    productFlavors {
        admin {
            signingConfig signingConfigs.release
            resValue "integer", "posts_to_display", "2"
        }
        customer {
            signingConfig signingConfigs.release
            resValue "integer", "posts_to_display", "5"
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("customer") && variant.buildType.name == 'release') {
            setIgnore(true)
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-vector-drawable:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.google.code.gson:gson:2.8.5'    //Gson
    implementation 'com.squareup.okhttp3:okhttp:3.13.0' //OkHttp
    implementation 'com.github.bumptech.glide:glide:4.8.0'  //Glide
    compileOnly 'org.projectlombok:lombok:1.18.4'   //lombok
    annotationProcessor 'org.projectlombok:lombok:1.18.4' //lombok
    implementation 'de.hdodenhof:circleimageview:3.0.0' //CircleImageView
    implementation 'com.google.dagger:dagger:2.17'  //Dagger
    annotationProcessor "com.google.dagger:dagger-compiler:2.17" //Dagger
}
